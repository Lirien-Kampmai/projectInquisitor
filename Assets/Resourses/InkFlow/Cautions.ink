VAR isPriest = false
VAR isWolf = false
VAR isGiveMoneyToBanditInForest = false
VAR isStealthInForest = false
VAR isAnswerToRolfInForest = false
VAR isRid = false
VAR isFirstTalkToRolf = false

->Cautions
===Cautions
-> path1GoldFate
== path1GoldFate
#scene isProlog
{valueGold > valueFate: ->path1Gold }
{valueGold <= valueFate: ->path1Fate }

=path1Gold
 Епископат выделил ему достаточно достойные деньги на покрытие дорожных расходов.#speaker Storyteller #pict Gate(edeldorf)
Умеренность - путь святых, но он пока не святой. Конраду хотелось бы иметь некоторый уровень удобства в дороге. Выделенных денег ему хватит на достойную еду и ночлег. На первые пару недель.#speaker Storyteller
Дороги королевства Эдельдорфского радовали своим комфортом. В основном потому что была середина лета. Конрад шёл по достаточно наезженным путям несколько недель, останавливаясь в каждом встреченном селении с церковью для проведения служб. #speaker Storyteller
В основном он проповедовал главам поселений, мелким аристократам и торговцам, но и обычных крестьян тоже не забывал, хотя их службы были уже гораздо короче.#speaker Storyteller #pict Sermon(path1)

Не забывал Конрад и о пожертвованиях...#speaker Storyteller

*[Принимал их с неохотой] Набожные правители поселений и торговцы предлагали мне помощь, на благо Церкви конечно же, и просили помолится за них. Я молился за каждого, но денег практически не брал, лишь изредка, чтобы было на что кушать в дороге.#speaker Konrad
->CautionsForRoadRolf

*[Принимал и распределял их нуждающимся] Набожные правители поселений и торговцы предлагали мне взять пожертвования на благо Церкви и просили помолится за них. Я молился за каждого, а пожертвования старался передать тем, кто в них нуждался, оставляя себе только необходимое.#speaker Konrad
->CautionsForRoadRolf
*[Принимал как должное.] Набожные торговцы предлагали мне взять пожертвования на благо Церкви и просили помолится за них. Я молился за каждого, принимая их пожертвования как должное. Мне ещё очень долго идти и деньги на благое дело всегда будут нужны.#speaker Konrad
->CautionsForRoadRolf
=CautionsForRoadRolf
Проводя службы среди уважаемых зверей, Конрад узнал много интересного. В частности - более короткий маршрут. Был только один нюанс - дороги были не столь безопасны, как ему хотелось бы.#speaker Storyteller
К его счастью, один добрый волк по имени Рольф, который был очень не добр к другим зверям, впечатлился его проповедью. Слова Конрада проникли глубоко в сердце волка.#speaker Storyteller

После службы Рольф предложил Конраду выпить вместе с ним. Конрад решил...#speaker Storyteller

*[Отказать. Уйти молиться] Нет, сын мой, я обязан вознести молитвы Господу...#speaker Konrad
-> NoFirstTalkToRolf
*[Согласится] Почему бы и нет? Слуга Господа обязан проводить время со своей паствой. Вечернюю  молитву прочитаю перед сном, думаю Господь не обидится. #speaker Konrad
->FirstTalkToRolf
=NoFirstTalkToRolf
Как скажете, святой отец. Берегите себя в пути, дороги нынче опасны - загадочно улыбнулся волк, прежде чем уйти.#speaker Rolf
Помолившись и переночевав в церкви, Конрад поутру отправился в дальнейший путь.#speaker Storyteller
->Brige
=FirstTalkToRolf
Поздним вечером...#speaker Storyteller #pict Evening(tavern)

Конрад пришёл в единственную таверну, которая была в небольшом поселении. Она выглядела не очень хорошо и не очень чисто. Некоторые старые брёвна уже гнили, придавая дому исхудавший вид. Он больше походил на притон сомнительных личностей, чем на место, достойное священника.#speaker Storyteller
Но Конрад достаточно смело шагнул внутрь. Рольф уже ждал его. Несмотря на поздний вечер, таверна была практически пуста. Лишь пару зверей, имевших скудный вид завсегдатаев, что-то распивали в противоположном конце зала.#speaker Storyteller
Волк улыбнулся, показывая свои жёлтые клыки. #speaker Storyteller

Великолепная служба, святой отец! Я бываю достаточно редко в церквях, но когда я в них бываю, то обычно святые отцы скупы на слова, исполняя свои повседневные ритуалы. Они не возносят, если вы понимаете о чём я. А у вас всё получилось. Особенно мне близки ваши слова о благотворительности.#speaker Rolf

Они не мои, сын мой -улыбнулся кабан - это слова Господа. #speaker Konrad
Но вы любите врагов ваших, и благотворите, и взаймы давайте, не ожидая ничего; и будет вам награда великая, и будете сынами Всевышнего; ибо Он благ и к неблагодарным и злым. Евангелие от Луки 6:32–35.#speaker Konrad

Да! Именно об этоих словах я и говорю! Зверям стоило бы чаще давать другим, не ожидая взамен ничего. Выпьем же за Божественное откровение для всех!#speaker Rolf

Кружка за кружкой, разговор довольно быстро перетёк в житейскую суету. Рольф рассказывал интересные истории из своих странствий, а Конрад старался больше слушать, чем говорить. Он подозревал, что Рольф - самый обычный бандит.#speaker Storyteller
Стезя бандита это, по мнению Церкви, несомненно плохо, но по мнению Инквизиции не являлось преступлением против Господа. Если звери, конечно, были набожными.#speaker Storyteller
Конрад был не особо против такого опасного знакомства, хотя его и терзали ощущения будущих последствий.#speaker Storyteller

Вечер плавно перетёк в ночь. Вполне себе неплохая беседа подходила к концу. Инквизитор узнал много интересных историй. Помимо прочего, Рольф дал ему пару советов о том, как себя вести в дороге, чтобы меньше привлекать внимания.
#speaker Storyteller
Конард весело подметил, что советы говорят, что перед ним как раз тот, чьего внимания надо избегать. На что Рольф только оскалился в загадочной улыбке.#speaker Storyteller

Выйдя на улицу Волк, уже изрядно захмелевший, обратился к Конраду #speaker Storyteller
Вы мне нравитесь. Что-то особенное в вас есть. Не хотелось бы, чтобы вы глупо сгинули после сегодняшнего вечера. Знаете, дороги нынче опасны. #speaker Rolf
Я дам вам совет. Если в течении недели пути вам попадутся волки и они будут намекать на проблемы, то скажите им, что вы мой друг и я буду разочарован, если у вас будут эти самые проблемы.#speaker Rolf
Но вы в любом случае берегите себя и берегитесь разбойников, святой отец!#speaker Rolf
~ isFirstTalkToRolf = true

Рольф загадочно подмигнул, похлопал кабана по плечу и разговор на этом был закончен.#speaker Storyteller
Утром, Конрад отправился в дальнейший путь.#speaker Storyteller
-> Brige

=path1Fate
Конрад покинул город ранним утром, едва взошло солнце. Епископат выделил ему сущие гроши, всего пару монет. Но он их не винил в жадности. #speaker Storyteller #pict Gate(edeldorf)
Умеренность - путь святых, хоть он ещё и не стал им. Выделенных денег ему хватит на простую еду и скромный ночлег первые пару ночей.#speaker Storyteller
Дороги королевства Эдельдорфского радовали своим комфортом. В основном потому что была середина лета. Конрад шёл по достаточно наезженным путям несколько недель, останавливаясь в каждом встреченном селении с церковью для проведения служб. #speaker Storyteller
В основном он проповедовал обычным крестьянам, но и глав поселений, мелких аристократов и торговцев тоже не забывал, хотя их службы были уже гораздо короче.#speaker Storyteller #pict Sermon(path1)

Не забывал Конрад и о пожертвованиях...#speaker Storyteller

*[Принимал их с неохотой] Набожные крестьяне предлагали мне помощь, на благо Церкви конечно же, и просили помолится за них. Я молился за каждого, беря деньги лишь изредка, чтобы было на что кушать в дороге.#speaker Konrad
->CautionsForRoadCivil

*[Принимал и распределял их нуждающимся] Набожные крестьяне предлагали мне помощь, на благо Церкви, и просили помолится за них. Я молился за каждого, а пожертвования старался передать тем, кто в них нуждался, не оставляя себе ничего.#speaker Konrad
->CautionsForRoadCivil

*[Принимал как должное.] Набожные торговцы предлагали мне взять пожертвования на благо Церкви и просили помолится за них. Я молился за каждого, принимая их пожертвования как должное. Мне ещё очень долго идти и деньги на благое дело всегда будут нужны.#speaker Konrad
->CautionsForRoadCivil
=CautionsForRoadCivil
Проводя службы среди простых зверей, Конрад узнал много интересного. В частности - более короткий маршрут. Был только один нюанс - дороги были не столь безопасны, как ему хотелось бы.#speaker Storyteller
К его счастью, один добрый заяц, чьего имени Конрад не запомнил, впечатлился его проповедью. Травоядный оказался опытным собирателем, научившим Конрада, как можно чуть тише ступать в лесу. #speaker Storyteller
~ isQuietStepSkill = true

Тепло попрощавшись с неравнодушным зайцем и получив предупреждение о разбойниках, поутру Конрад отправился в дальнейший путь.#speaker Storyteller
->Brige


==Brige
Прошло несколько дней в пути, молитвах и раздумьях. #speaker Storyteller #pict Forest

Путь молодого инквизитора лежал в замок Вуфсбург, где можно остановится на ночлег у барона Торстейна. До него было три дня пути. Но крестьяне указали ему короткий путь, всего за один день, через лес и деревушку у реки. Но оба места были опасными.#speaker Storyteller
Конрада это не испугало.#speaker Storyteller

Он пошёл короткой дорогой. По обе стороны раскинулся плотный лес, скрывающий тенями своей листвы всё что лежало дальше трёх-четырёх десятков шагов. Сама дорога была ярким пятном среди зелёного моря, хорошо освещаемая послеполуденным солнцем.#speaker Storyteller
Большая часть леса уже была позади. Никакой беды не случилось. Это успокаивало Конрад решил... #speaker Storyteller

*[Идти осторожно] Я внял предупреждениям о разбойниках и буду идти более осторожно, внимательно смотря по сторонам. #speaker Konrad 
->path1Carefully
*[Идти и молится] Я внял предупреждениям о разбойниках, но на всё воля Господа. Он защитит меня в пути. #speaker Konrad 
->path1Pray

=path1Carefully
Благодаря осторожности, он заметил нечто необычное. Впереди, около небольшого поворота дороги, за плотными кустами, виднелись две неизвестные фигуры. Их выдавал шелест. Среди общего шума леса это можно было легко не заметить, если не быть бдительным.#speaker Storyteller
Это могли быть обычные путники, остановившиеся отдохнуть. Или разбойники, поджидающие путников. Конрад не мог сказать наверняка. Он знал лишь, что впереди кто-то есть.#speaker Storyteller

Он решил...#speaker Storyteller

*[Это разбойники] Это наверняка разбойники. Обойду их скрытно через лес!#speaker Konrad 
->Stealth
*[Это путники] Это, возможно, обычные путники, иначе бы они сидели скрытно и я бы их не заметил. Пойду по дороге, не скрываясь и уповая на Господа.#speaker Konrad 
-> path1Pray
=Stealth
Он продвигался тихи и осторожно, сильно обходя стороной двух неизвестных, стараясь чтобы они не заметили его. #speaker Storyteller 
{isQuietStepSkill == true: Конрад старался использовать советы по тихому шагу и у него вполне получалось. Ни одна ветка не хрустнула под его лапами. ->NoNoDetection}

Он почти обошёл их, когда какая-то ветка под его лапой предательски хрустнула...#speaker Storyteller 

*[Бежать!] Я не размышлял ни секунды- бежать! Господь придаст мне сил. #speaker Konrad
->Detection
*[Затаиться!] Я не размышлял ни секунды- затаиться! Господь не даст меня обнаружить. #speaker Konrad
->NoDetection
=Detection
Он так быстро не бегал, наверное, никогда. Позади раздался волчий вой. Глупо бегать от волков. Но Конрад был священником Церкви. В его сердце всегда была частица Господа, которая его берегла, а если с тобой Господь, то бежать от волков уже не так глупо.#speaker Storyteller 
Инквизитор остановился лишь когда понял, что не слышит звуков позади себя. Оглянувшись, он не увидел никакой погони.#speaker Storyteller 

Господь оберегающий, спасибо тебе - тяжело дыша, выдохнул Конрад, перекрестившись.#speaker Konrad
->TalkIntoBrigeFight
->END
=NoDetection
В лесу было столь тихо, что Конрад мог услышать даже своё дыхание. Двое неизвестных были где-то рядом...#speaker Storyteller 

*[Понадеяться на удачу] Конрад затаился, надеясь что Господь его сбережёт. Он даже молитву побоялся прочитать. И это помогло. Неизвестные вернулись к своим кустам. Опасность миновала.#speaker Storyteller 
-> TalkIntoBrigePeace

*[Помолится] Конрад, едва шевеля губами, беззвучно зашептал молитву, прося Господа уберечь его от напастей.#speaker Storyteller 
->DetectionFail
=NoNoDetection
Тихо обойдя неизвестных, Конрад спустя время перестал скрываться.
->TalkIntoBrigePeace
=DetectionFail
Неожиданно для себя, он закончил молитву своим обычным голосом, будто бы не прятался, а проводил службу в храме. Привычка благодарить Господа сыграла с ним злую шутку - его заметили!#speaker Storyteller 
->Detection

=path1Pray
Господь, несомненно, защищает своих верующих, но стоило всё-же быть более бдительным. Путевую молитву инквизитора прервали два волка, появившихся из-за кустов дороги. #speaker Storyteller
Они выглядели бедновато, но при оружии. Один многозначительно держал топорик, а другой нечто, похожее на короткий меч, только немного погнутый и изьеденный ржавчиной. Оба были закутаны в истрёппанные временем и непогодой плащи.
К Конраду подошёл один из них и недобро улыбнулся. #speaker Storyteller

Путник, проход в этих местах не безопасный. Так что если не хочешь встретиться с бандитами, доставай кошель.#speaker Wolf

Отдавать свои скромные пожитки инквизитор совсем не хотел. #speaker Storyteller
~ isWolf = true

*[Представится как инквизитор] Я инквизитор и я требую вашего покаяния! Раскайтесь в своём грехе и Господь простит вас, как и я. #speaker Konrad
->deathPath1
*[Попробовать вызвать жалость] Я простой паломник, у меня почти ничего нет. Я иду в святой город, не убивайте, ради Господа!#speaker Konrad
->giveMoneyToBandit
*[Представится как священник] Я священник церкви, неужели вы ограбите святого отца?#speaker Konrad
~isPriest = true
->giveMoneyToBanditExtendet
*[Попытаться их обмануть] Я иду по важному поручению. Вот мои грамоты! Если вам нужны деньги - истребуйте у лорда этих земель от имени Конрада. Скажете, что помогли мне в пути.#speaker Konrad
->liePath1
*{isFirstTalkToRolf == true}[Вспомнить слова Рольфа] Я друг Рольфа. Он просил передать, что будет недоволен, если со мной что-то случится.#speaker Konrad
~ isRid = true
~ isAnswerToRolfInForest = true
->RidPath1
=deathPath1
Инквизитор? Нам такие проблемы не нужны...# speaker Wolf
Неожиданно, волки накинулись на Конрада. Он слишком поздно понял, что не стоит говорить бандитам посреди леса. Никто не наёдёт его истерзанное тело среди деревьев. Последнее что он почувствовал, как топор раскалывает его голову. #speaker Storyteller #pict Death
->ToTheDeath
=giveMoneyToBandit
Мы заметили, что ты не торговец. Тем не менее, проход всё равно платный, как и попытка уйти обратно. Так что постарайся найти деньги прямо сейчас. Ты же не хочешь неприятностей в лесу, верно?#speaker Wolf
-> giveMoneyToBanditExit
=giveMoneyToBanditExtendet
Священники - звери богатые. Поделитесь с нами благом Господа, святой отец и уйдёте с миром.#speaker Wolf
->giveMoneyToBanditExit
=giveMoneyToBanditExit
Держите свои деньги...#speaker Konrad
~ isGiveMoneyToBanditInForest = true
{isPriest == false: Видишь, путник, было совсем не сложно. Ну бывай! #speaker Wolf}
{isPriest == true: Видите, святой отец, вам было совсем не сложно, а нам сегодня Господь послал денег на хорошую гулянку! Ну бывай, святой отец!#speaker Wolf}
->TalkIntoBrigePeace
=liePath1
Ого, важный зверь! Ваше милость, извините что не признали сразу друзей барона. Проходите, нам не нужны проблемы с милордом Торстейном. Оставьте грамоты себе, мы всё равно читать не умеем.#speaker Wolf

Довольный, Конрад поспешил убраться поскорее, стараясь не думать, чем так страшен местный барон. Но он не успел уйти далеко. Едва он отошёл от волков, как его моего слуха донёсся недовольный крик волка.#speaker Storyteller

Дурень! Барон же писать и читать не умеет! Какие к чёрту грамоты!? Он должен был назвать по имени нашего вожака. Это наверняка был какой-нибудь святоша или торгаш! За ним! В погоню!#speaker Wolf
->Detection
=RidPath1
Ваше милость, извините что не учуяли сразу друзей нашего вожака. Меня зовут Рид. Мы сегодня собираемся в деревушку у реки, не составите нам вечером компанию? Староста - наш друг. Друзей Рольфа мы угостим бесплатно. #speaker Rid

Волк, с которым инквизитор провёл вечер несколько дней назад, оказался вожаком целой стаи бандитов! Конрад предполагал, что Рольф - зверь опасный, но теперь он в этом убедился.#speaker Storyteller
Сам Отец всевышний направил Конрада на лёгкий путь через лес. Он пообещал подождать их в деревне. Лгал ли он при этом - знал только Господь. Рид тепло попрощался с инквизитором и он отправился дальше в путь.#speaker Storyteller
~isRid = true
->TalkIntoBrigePeace

=TalkIntoBrigePeace

{isGiveMoneyToBanditInForest == true: Его не надо было уговаривать уходить дважды.#speaker Storyteller }
{isStealthInForest == true: Он быстро пошёл вперёд, пока никто его не заметил. #speaker Storyteller } 
{isAnswerToRolfInForest == true: Он пошёл дальше, уверенный в своей безопасности, хранимый Господом и, что удивительно, бандитами. #speaker Storyteller}

Святой отец ускоренным шагом двинулся дальше по дороге. Лес довольно быстро стал меняться, переходя в травянистую степь и разнотравные, засеянные крестьянами, поля. Вдалеке, извиваясь тёмными синими водами, лежала река, а за ней виднелись крыши нескольких домов. #speaker Storyteller #pict Brige(brige)
Приближаясь ближе к деревне, Конрад хорошо разглядел реку - быструю, полную острых камней и рыбы. Он бы не хотел оказаться в такой реке. Но выглядела она прекрасно.#speaker Storyteller
Тем удивительнее было не увидеть никого из местных жителей с рыбацкими снастями. Инквизитор даже остановился, чтобы помолится. Всё, что он просил у всевышнего - чтобы это была просто странная деревня.#speaker Storyteller
Он не боялся ереси или агрессии. Но при мысли, что за спиной нет братьев по вере, он начинал волноваться. Справится ли он один, когда встретится лицом к лицу с еретиком? Конрад предпочитал об этом не думать. На всё воля Господа.#speaker Storyteller

Заходя на каменный, покрытый во множестве мест мхом, мост, Конрад увидел на другом его конце странного зверя. При приближении он разглядел в нём ящера. Чёрного, будто сама ночь, одетого в простые серые одежды, он лениво осматривал окресности своими устрашающими глазами, которые, пылали внутренним огнём.#speaker Storyteller #pict Lizard
Он стоял, прислонившись к дорожному столбу, на другом конце моста и словно кого-то ждал. Завидев кабана, ящер даже не пошевелился, но стоило Конраду подойти ближе, как незнакомец оживился и сделал шаг навстречу.#speaker Storyteller

Биллафалат Факал - ящер протянул свою лапу, представляясь. #speaker Fakar #pict Brige(brige)

Конрада охватил страх.#speaker Storyteller

Ящеров он и раньше видел, но никогда - столь огромных. Размерами он доходил почти до медведя. Всю его кожу на руках и морде покрывали прочные на вид бугорки, делая её чём-то похожей на кольчугу. В сравнении с ним, Конрад выглядел маленьким, едва выше пояса.#speaker Storyteller
И теперь этот большой, диковинный хищник, не выговаривающий букву "Р" вышел к нему и просто здоровается. Для него это было очень необычно. И подозрительно.#speaker Storyteller

*[Представится по имени] Конрад. Просто Конрад. Паломник и священник святой матери Церкви. Зачем вы подошли ко мне? Вы кого-то ждёте?#speaker Konrad
->TalkToFakar

*[Представится по титулу] Инквизитор Конрад. Зачем вы подошли ко мне? Вы кого-то ждёте? #speaker Konrad
~ isInquisition = true
->TalkToFakar
=TalkToFakar
{isInquisition == true: Инквизитол? Моё почтение Целкви, святой отец - едва заметно, но с уважением, наклонил морду ящер.#speaker Fakar}

Я устал стоять и ничего не делать. И вы плавы, жду святой отец. Волков не встлечали по дологе?#speaker Fakar

*{isWolf == false}[Вспомнить о пустниках]Волков? Нет, не встречал. Но в лесу видел каки-то путников. Я их не разглядел, может быть это и были волки.#speaker Konrad
->TalkToFakarInfoNoWolf
*{isWolf == true }[Быть осторожным] Да, встречал каких-то волков. Они берут плату за проход по дороге.#speaker Konrad
->TalkToFakarInfoRid
*{isWolf == true }[Быть прямолинейным]Да, встречал. Бандитов. Они берут плату за проход по дороге, но явно это не звери местного лорда. Зачем вы их ждёте?#speaker Konrad
->TalkToFakarInfoRid
-
*{isRid == true  }[Рассказать о Риде] Да, встречал. Они берут плату за проход по дороге, бандитствуют, хотя выглядят скорее нищими. Меня не тронули. Одного из них зовут Рид. Зачем вы их ждёте?#speaker Konrad
->TalkToFakarInfoRid
=TalkToFakarInfoNoWolf
Можете быть увеленны - это они и были. Чёлтовы бандиты. Я хочу с ними лазоблаться. По личным пличинам. Но это не важно. Холошо, что вы с ними не встлечались.#speaker Fakar
Святой отец, не составите сегодня вечером мне компанию? Местный сталоста дал мне плиют, у него отличный дом. Я увелен, он будет лад плинять священника.#speaker Fakar

Конрад был не особо против. Он чувствовал, что ему стоит остаться, провести службу в деревне. Да и просто хотелось получше узнать странного ящера. Откуда он пришёл? Почему он такой большой? Правильно ли он верит в Бога?#speaker Storyteller

Инквизитор согласился остаться до вечера. Факар обьяснил ему, как найти дом старосты. Конрад отправился туда, а ящер остался дальше ждать на мосту.#speaker Storyteller

Вечер обещал стать очень интересным. Для Конрада. И для инквизитора. #speaker Storyteller
-> EndCautions
=TalkToFakarInfoRid
Я хочу с ними лазоблаться. По личным пличинам. А почему они вас не тлонули? Только не влите, пожалуйста, что вы обычный звель. Просто так они никого не отпускают. Это их земли.#speaker Fakar

Его лапа довольно показательно легла на топорик, висевший на поясе. Он выглядел спокойным, но Конрад не сомневался, что если он ответит нееправильно, то паломничество может стремительно закончится в реке. Или на тарелке ящера.#speaker Storyteller
Конрад разумно решил рассказать о своём знакомстве с Рольфом, а о себе...#speaker Storyteller

*{isInquisition == false}[Честно] Вы очень умны, господин Факар. Я - член святой палаты расследования ересей! Инквизитор, совершающий паломничество, которому в странствиях повезло познакомится с их вожаком - Рольфом. Он и сказал мне, что нужно говорить местным бандитам.#speaker Konrad
~isInquisition = true
->EndTalkToFakarInfoRid

*[Честно и скромно] Вы очень умны. Ваша правда, я не просто паломник, я странствующий священник, которому в странствиях повезло познакомится с их вожаком - Рольфом. Он и сказал мне, что нужно говорить местным бандитам.#speaker Konrad
->EndTalkToFakarInfoRid
=EndTalkToFakarInfoRid
{isInquisition == true: Инквизитол? Моё почтение Целкви, святой отец - едва заметно, но с уважением, наклонил морду ящер.#speaker Fakar}

Значит, его зовут Лольф. Когда-нибудь я добелусь и до него... Чтож, святой отец, если у вас нет слочных дел, мы могли бы поговолить в более плиятной обстановке. Мне дал плиют местный сталоста, у него отличный дом.#speaker Fakar

Конрад согласился. Факар показался ему интересным зверем. Он никогда не видел таких гигантов. Впереди его ждала очень интересная беседа беседа, которая изменит жизнь молодого инквизитора. Но он этого пока не знал.#speaker Storyteller
-> EndCautions

=TalkIntoBrigeFight
Святой отец ускоренным шагом двинулся дальше по дороге. Лес довольно быстро стал меняться, переходя в травянистую степь и разнотравные, засеянные крестьянами, поля. Вдалеке, извиваясь тёмными синими водами, лежала река, а за ней виднелись крыши нескольких домов.#speaker Storyteller #pict Brige(brige)
Умиротворяющую картину испортил вой позади. Не где-то вдалеке, как это было в лесу, а ближе. Гораздо ближе, чем хотелось бы Конраду. Сердце святого отца сжало от страха, но лапы уже несли его в сторону деревни.#speaker Storyteller 
Сначала он хотел спастись в водах реки. Волки известны своей нелюбовью к воде. Больше них воду не любят только, возможно, кошки. Но подбегая ближе, он понял, что идея глупая. Течение было очень быстрым и оказались видны острые камни, усеявшие берег.#speaker Storyteller 
Невдалеке виднелся старинный каменный мост. К нему-то Конрад и ринулся, крича о помощи. Крики около поселения должны были спугнуть волков, но они лишь ускорились. Он уже слышал, как их лапы перебирают по траве, слышал их прерывистое дыхание. Они догоняли.#speaker Storyteller 
Вбегая на мост, он увидел на другой его стороне огромного зверя. Чёрного, будто сама ночь, ящера, одетого в простые серые одежды. Даже издалека Конрад разглядел устрашающие глаза, которые, как ему показалось, пылали внутренним огнём.#speaker Storyteller #pict Lizard
Он стоял, прислонившись к дорожному столбу, на другом конце моста, но увидев Конрада ринулся навстречу, с топориком в одной лапе и коротким копьём в другой. Хотя ринулся - это громко сказано. Скоростью ящеры не отличались.#speaker Storyteller 

Помогите, ради Господа! - выкрикнул Конрад, вбегая на мост. #speaker Konrad

Больше он ничего не успел сказать. Погоня настигла его. Когтистая лапа, раздирая одежду на спине, повалила его, болезненно прижав морду к камням моста. Один из волков запрыгнул ему на спину и угрожающе зарычал.#speaker Storyteller #pict Brige(brige)

Это наша добыча!# speaker Wolf
А вы - моя! - холодно ответил им ящер. #speaker Lizard

Волки не испугались и ринулись вперёд, хотя и не столь рьяно, как при погоне.#speaker Storyteller

Конрад мог бы попробовать остановить кровопролитие...#speaker Storyteller

*[Промолчать] Но он не смог сказать ни слова. #speaker Storyteller
->RidDeath
*[Прекратить!] Именем Инквизиции! Остановитесь! #speaker Konrad
~isInquisition = true
->RidSurvive
=RidDeath
Последовал короткий свист, единичный звон клинка, четыре удара, сопровождаемые визгами волков и наступила тишина, нарушаемая лишь звуками текущей реки и бульканьем. Конрад поднял морду и увидел как ящер, переступая через дёргающихся в агонии волков, подходит к нему.#speaker Storyteller
Всего пару секунд назад они были живы, а теперь гарантированно мертвы, хоть ещё и дёргались. У одного из них в груди торчало копьё, а из разрубленного горла хлестала кровь. У другого была перерублена пасть и кисть лапы, а между ног потокоми сбегала кровь.#speaker Storyteller

Ящер стёр кровь с топорика и руки куском какой-то тряпки, и протянул ещё горячую лапу Конраду.#speaker Storyteller

Биллафалат Факал. #speaker Fakar

Конрад неуверенно пожал лапу и полностью представился, умолчав об инквизиторском сане и с изумлением осматривая Факара. # speaker Storyteller
Ящеров он и раньше видел, но никогда - столь огромных. Размерами он доходил почти до медведя. Всю его кожу на руках и морде покрывали прочные на вид бугорки, делая её чём-то похожей на кольчугу. В сравнении с ним, Конрад выглядел маленьким, едва выше пояса. # speaker Storyteller

Спасибо что спасли мне жизнь. #speaker Konrad

Я бы не поблезговал благодалностью от Церкви. Не денежной. Но об этом позже. Пледлагаю поговолить в более плиятном месте. Мне дал плиют местный сталоста, у него отличный дом.#speaker Fakar

Я не против. Наверное.#speaker Konrad

Факар перекинул тело первого волка в реку, через парапет моста, и вытащил копьё из второго волка. Он уже умер от кровотечения. Факар забросил его себе на плечо, как будущий обед.# speaker Storyteller

Плойдёмте со мной, инквизитол. И не делайте, пожалуйста, такую молду. Мне надо что-то есть. #speaker Fakar

Конрад не мог не согласится. Факар был хищником, травяным супом он сыт явно не будет.# speaker Storyteller
Следуя за Факаром, инквизитор углубился вглубь деревни...# speaker Storyteller
->EndCautions
=RidSurvive
Последовал короткий свист, единичный звон клинка и наступила тишина, нарушаемая лишь звуками текущей реки и болезненным скулежем одного из волков.#speaker Storyteller
 Конрад поднял морду и увидел как ящер стоит, держа одного волка за горло. Из-под его лапы сочилась кровь - когти ящера были сродни кинжалам. Другой же волк стоял на коленях в метре от ящера, держась за древко копья, пробившего ему грудь. Всё что он мог - это скулить о пощаде.#speaker Storyteller
 
 Я даже не знаю, повезло ли тебе - произнёс ящер волку - Но ты ещё можешь умелеть как воин. Только поплоси... #speaker Lizard

Ящер высунул длинный раздвоеный язык и попробовал кровь волка на вкус, недвузначно оскалившись. Он был готов впиться своими устрашающими зубами прямо в морду волка, стоило только дать повод. Или попросить.

...пощады... - едва выдохнул волк #speaker Wolf

*[Строгое наказание]Он будет осуждён за нападение на инквизитора и понесёт самое строгое наказание. #speaker Konrad
->PunishmentRid

*[Простить грешника]Любите врагов ваших, творите добро ненавидящим вас, и молитесь за творящих вам напасть и гонителей, и будете сынами Отца вашего, Господа на небе, ибо солнце Своё поднимает он на злых и добрых, и посылает дождь на праведных и неправедных.#speaker Konrad
-> ForgivenessRid
=PunishmentRid
Всё же не повезло тебе. - сказал волку ящер - Наказание так наказание. #speaker Lizard

Когтистая лапа сжалась на горле несчастного волка, а затем ящер вырвал целый кусок, который проглотил так, словно это был обычный обед. После чего спокойно перекинул волка через парапет моста, прямо в реку.#speaker Storyteller

Зачем? Я ведь сказал про суд, а это казнь!#speaker Konrad

Потому что я посчитал это сплаведливым. Вы хотели его наказать - я его наказал.#speaker Lizard

Ящер вытер окровавленную пасть и руки куском какой-то тряпки, и протянул ещё горячую от крови лапу Конраду.#speaker Storyteller

Биллафалат Факал, плиятно познакомится с инквизицией. #speaker Fakar

Конрад неуверенно пожал лапу и полностью представился, с изумлением осматривая Факара. # speaker Storyteller
Ящеров он и раньше видел, но никогда - столь огромных. Размерами он доходил почти до медведя. Всю его кожу на руках и морде покрывали прочные на вид бугорки, делая её чём-то похожей на кольчугу. В сравнении с ним, Конрад выглядел маленьким, едва выше пояса. # speaker Storyteller

Спасибо что спасли мне жизнь. #speaker Konrad

Я бы не поблезговал благодалностью от инквизиции. Не денежной. Но об этом позже. Пледлагаю поговолить в более плиятном месте. Мне дал плиют местный сталоста, у него отличный дом.#speaker Fakar

Я не против. Но больше не надо никого убивать.#speaker Konrad

Кломе ещё одного - больше никого, святой отец - хищно улыбнулся ящер.#speaker Fakar

Факар вытащил копьё из второго волка. Он ещё не умер, но уже не мог сопротивляться. Перебив ещё живому лапы, Факар забросил его себе на плечо, как будущий обед.# speaker Storyteller

Плойдёмте со мной, инквизитол. И не делайте, пожалуйста, такую молду. Мне надо что-то есть. #speaker Fakar

Конрад не мог не согласится. Факар был хищником, травяным супом он сыт явно не будет.# speaker Storyteller
Следуя за Факаром, инквизитор углубился вглубь деревни...# speaker Storyteller
-> EndCautions
=ForgivenessRid
Всё же повезло, волк. Господь на твоей столоне. Сегодня. - хищно улыбнулся ящер. #speaker Lizard

Пока Конрад поднимался и отряхивался от дорожной пыли, ящер связал лапы волка импровизированной тряпкой и сам подошёл к инквизитору. #speaker Storyteller

Биллафалат Факал - ящер протянул свою лапу. - плиятно познакомится с инквизицией.#speaker Fakar

Конрад пожал лапу и полностью представился, с изумлением осматривая Факара.# speaker Storyteller
Ящеров он и раньше видел, но никогда столь огромных. Размерами он доходил почти до медведя. Всю его кожу на руках и морде покрывали прочные на вид бугорки, делая её чём-то похожей на кольчугу. В сравнении с ним, Конрад выглядел маленьким, едва выше пояса. # speaker Storyteller

Спасибо что спасли мне жизнь. #speaker Konrad

Я бы не поблезговал благодалностью от инквизиции. Не денежной. Но об этом позже. Пледлагаю поговолить в более плиятном месте. Мне дал плиют местный сталоста, у него отличный дом.#speaker Fakar

Я не против. Но мне нужно пару минут.#speaker Konrad

Конрад подошёл к связанному волку. Он выглядел весьма бедным. Одежда имела цвета грязи, полы плаща были изорваны, а ремни явно были с кого-то сняты и не подходили по размеру. Он выглядел жалко.# speaker Storyteller
С его морды едва заметной струйкой стекала кровь. На ней была свещая но неглубокая рана, прямо поперёк, ближе к глазам. Конрад понял, как же волку повезло. Промедли он всего мгновение, меньшее чем удар сердца, и тогда Факар перерубил бы ему морду.# speaker Storyteller

Как тебя зовут?#speaker Konrad

Рид, господин инквизитор. Спасибо вам, что не дали умереть. Я буду молится за вас каждый день, благодаря Господа за вашу доброту. #speaker Rid

Я готов простить тебя, сын мой, но ещё не простил полностью. Мы решим твою судьбу чуть позже. Не пытайся сбежать. Милость Господа безгранична, а вот милость господина Факара - нет. Но молится можешь уже сейчас. Господу будет приятно услышать твой голос.#speaker Konrad

Возьмём его с собой, я решу что с ним делать.#speaker Konrad

Факар уже вытащил копьё из второго волка. Он ещё не умер, но не мог сопротивляться. Перебив ещё живому лапы, Факар забросил его себе на плечо. Как трофей. Или как обед.# speaker Storyteller

Конечно, инквизитол. Плойдёмте со мной. Вам вести вашего пленника.#speaker Fakar

Ослабив путы на ногах, чтобы можно было ходить, Конрад повёл Рида вслед за Факаром, куда-то в деревню...# speaker Storyteller
-> EndCautions


==EndCautions
->ToTheContinue